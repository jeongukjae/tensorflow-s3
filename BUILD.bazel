load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python//python:packaging.bzl", "py_package", "py_wheel")
load("@rules_python//python:versions.bzl", "gen_python_config_settings")
load("@python_version_repo//:py_version.bzl", "REQUIREMENTS")
load("//tools:whl.bzl", "make_variable_tags")

compile_pip_requirements(
    name = "requirements",
    extra_args = [
        "--allow-unsafe",
        "--build-isolation",
        "--rebuild",
    ],
    generate_hashes = True,
    requirements_in = "requirements.in",
    requirements_txt = REQUIREMENTS,
)

make_variable_tags(name = "make_variable_tags")

gen_python_config_settings()

py_wheel(
    name = "tensorflow_s3_wheel",
    abi = "$(ABI)",
    distribution = "tensorflow_s3",
    python_tag = "$(PYTHON_TAG)",
    version = "0.0.1",
    platform = select({
        ":aarch64-apple-darwin": "macosx_11_0_arm64",
        ":aarch64-unknown-linux-gnu": "manylinux2014_aarch64",
        ":x86_64-apple-darwin": "macosx_11_0_x86_64",  # this is typically macosx_10_9_x86_64?
        ":x86_64-pc-windows-msvc": "win_amd64",
        ":x86_64-unknown-linux-gnu": "manylinux2014_x86_64",
    }),
    toolchains = [":make_variable_tags"],
    deps = ["//tensorflow_s3"],
)
